# Description:
#   å¤©æ°—äºˆå ±
# Commands:
#   hubot ä»Šæ—¥ã®å¤©æ°—ã‚’æ•™ãˆã¦ - 16æ™‚é–“åˆ†ã®å¤©æ°—äºˆå ±
#   hubot ä»Šé€±ã®å¤©æ°—ã‚’æ•™ãˆã¦ - 7æ—¥é–“åˆ†ã®å¤©æ°—äºˆå ±

Forecast = require('forecast.io')
moment = require('moment-timezone')

class ForecastBot
  constructor: ->
    @hourly_limit = 16
    @daily_limit = 7
    @latitude = process.env.HUBOT_FORECAST_LAT
    @longitude = process.env.HUBOT_FORECAST_LONG
    @timezone = process.env.HUBOT_FORECAST_TZ

  start: (robot)->
    robot.respond /(?:ä»Šæ—¥ã®)?å¤©æ°—/, (res)=>
      res.send 'ã‚“ã€‚'
      @get_forecast res, (data)=>
        hourly_data = data.hourly.data.filter((d,i)=>i<@hourly_limit)
        lines = @make_lines(hourly_data, 'hh:mm')
        message = 'ã“ã‚“ãªæ„Ÿã˜ã ã‚ˆãƒ¼ã€‚\n' + lines.join('\n')
        res.reply message

    robot.respond /(?:ä»Šé€±|ä¸€é€±é–“)ã®å¤©æ°—/, (res)=>
      res.send 'ã‚“ã€‚'
      @get_forecast res, (data)=>
        daily_data = data.daily.data.filter((d,i)=>i<@daily_limit)
        lines = @make_lines(daily_data, 'M/DD')
        message = 'ã“ã‚“ãªæ„Ÿã˜ã ã‚ˆãƒ¼ã€‚\n' + lines.join('\n')
        res.reply message

  get_forecast: (res, callback)->
    Forecast.get
      APIKey: process.env.HUBOT_FORECAST_API_KEY
      latitude: @latitude
      longitude: @longitude
      units: 'si'
      onsuccess: (data)=>
        callback(data)
      onerror: (err)->
        console.error err
        res.reply 'ã”ã‚ã‚“ã€ãˆã‚‰ã£ãŸï¼'

  make_lines: (data, time_format)->
    message = data.map (d)=>
      time = moment.tz(d.time*1000, @timezone).format(time_format)
      temp = @get_tempreture(d)
      precip = Math.floor(d.precipProbability*100)
      precip = " #{precip}" if precip < 10
      icon = @get_weather_icon(d.icon)
      summary = d.summary
      "#{time} #{icon} #{temp}â„ƒ #{precip}% #{summary}"

  get_tempreture: (data)->
    if data.temperature
      @n_to_s(data.temperature)
    else if data.temperatureMin
      "#{@n_to_s(data.temperatureMin)}ï½ž#{@n_to_s(data.temperatureMax)}"

  n_to_s: (n)->
    n = Math.floor(n*10)/10
    n = "#{n}.0" if n is Math.floor(n)
    n = " #{n}" if n < 10 and n >= 0
    n

  get_weather_icon: (name)->
    switch name
      when 'clear-day', 'clear-night'
        'â˜€'
      when 'rain'
        'â˜‚'
      when 'snow', 'sleet'
        'â˜ƒ'
      when 'fog', 'cloudy'
        'â˜'
      when 'partly-cloudy-day', 'partly-cloudy-night'
        'â›…'
      when 'wind'
        'ðŸƒ'
      else
        'ï¼Ÿ'

module.exports = (robot)->
  forecast = new ForecastBot()
  forecast.start(robot)

module.exports.ForecastBot = ForecastBot
